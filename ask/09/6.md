**plik wykonywalny** -- plik, który może być uruchomiony bezpośrednio w środowisku systemu operacyjnego.
**nagłówek pliku ELF** -- pierwsze 64 bajty (56 na 32 bitach) pliku ELF. Zawiera m. in. magiczną liczbę i informacje o architekturze potrzebne do uruchomienia pliku i gdzie znaleźć **nagłówek programu**.
Z `readelf -h main` wyczytujemy:
```Entry point address:               0x400122
```
W `objdump -d main` widzimy, że `0x400122` to adres pierwszej instrukcji `_start`, czyli `push %rax`.
**nagłówek programu** -- część pliku ELF mówiąca, w jaki sposób utworzyć proces, jako który ma działać program.
Z `readelf -l main` wyczytujemy, że sekcja `.text` będzie w segmencie 01 załadowanym pod adresem `0x400000`.
**segment** -- kawałek pamięci wirtualnej procesu. Informacje o segmentach znajdują się w nagłówkach programu. Do segmentu może być załadowane wiele sekcji.
**skrypt konsolidatora** opisuje format i układ sekcji w wyjściu konsolidatora. Każda konsolidacja jest kierowana przez taki skrypt, ale zwykle jest używany skrypt automatycznie wygenerowany przez konsolidator.

```
OUTPUT_FORMAT("elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(_start)
PHDRS /* program headers */
{
/* flagi opisują read/write/executable analogicznie jak w systemie plików */
/* PT_LOAD oznacza, że segment będzie załadowany z pliku */
  code   PT_LOAD FLAGS(5);
  rodata PT_LOAD FLAGS(4);
  data   PT_LOAD FLAGS(6);
}
SECTIONS
{
  . = 0x400000 + SIZEOF_HEADERS;

  .text :
  {
    *(.text .text.*)
  } : code

  .rodata :
  {
    *(.rodata .rodata.*)
  } : rodata

  .data :
  {
    *(.data .data.*)
  } : data

  .bss :
  {
   *(.bss .bss.*)
   *(COMMON)
  } : data

  /DISCARD/ :
  {
    *(.note.gnu.property)
  }
}
```
